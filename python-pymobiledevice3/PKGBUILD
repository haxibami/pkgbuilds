# Maintainer: haxibami <contact at haxibami dot net>

pkgname=python-pymobiledevice3
_pkgname=pymobiledevice3
pkgver=7.0.5
pkgrel=1
pkgdesc='Pure python3 implementation for working with iDevices'
arch=('any')
url='https://github.com/doronz88/pymobiledevice3'
license=('GPL-3.0-or-later')
depends=(
  'ipython'
  'libusb'
  'openssl'
  'python'
  'python-asn1'
  'python-bpylist2'
  'python-coloredlogs'
  'python-construct'
  'python-construct-typing'
  'python-cryptography'
  'python-daemonize'
  'python-developer_disk_image'
  'python-fastapi'
  'python-gpxpy'
  'python-hexdump'
  'python-hyperframe'
  'python-ifaddr'
  'python-inquirer3'
  'python-ipsw_parser'
  'python-nest-asyncio'
  'python-opack2'
  'python-packaging'
  'python-parameter-decorators'
  'python-pillow'
  'python-plumbum'
  'python-prompt_toolkit'
  'python-psutil'
  'python-pycrashreport'
  'python-pygnuutils'
  'python-pykdebugparser'
  'python-python-pcapng'
  'python-pytun-pmd3'
  'python-pyusb'
  'python-qh3'
  'python-requests'
  'python-srptools'
  'python-sslpsk-pmd3'
  'python-tqdm'
  'python-typer'
  'python-typer-injector'
  'uvicorn'
  'xonsh'
)
makedepends=('python-build' 'python-wheel' 'python-installer' 'python-setuptools' 'python-setuptools-scm')
checkdepends=('python-pytest' 'python-pytest-asyncio')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('a63b05ec84ac3693a8c3b924efd56a4cf41d1ab06370c08d6739a963c27b3360')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  export SETUPTOOLS_SCM_PRETEND_VERSION="$pkgver"
  python -m build --wheel --no-isolation
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python -m pytest -m cli
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
