# Maintainer: haxibami <contact at haxibami dot net>

pkgname=hollo
pkgver=0.6.14
pkgrel=1
pkgdesc="Federated single-user microblogging software"
arch=("x86_64" "aarch64")
url="https://github.com/fedify-dev/hollo"
license=('AGPL-3.0-or-later')

makedepends=("git" "npm")
depends=("nodejs" "pnpm")
optdepends=(
  "caddy: for reverse proxy"
  "nginx: for reverse proxy"
)

install="hollo.install"

source=(
  "hollo-${pkgver}::git+${url}.git#tag=${pkgver}"
  "hollo.service"
  "hollo.sysusers"
  "hollo.tmpfiles"
  "hollo.install"
  "hollo.hook"
)

sha256sums=('999161f4b55e1f75b6f904142a3f87684ff3a124881d35baa4b3ef16e2ef057e'
            'cb940e0cffaaf1756783017bfbd06d8a00e5ef0928f4f500c26f9558dd52cd14'
            '0c7fcd560f0eeb9fc532131e5e0d15f5502abc12f0d5588ad575e2f74787b3ac'
            '46f0144b70687c42b9a87abbc9e5d0a3341c0893a163947a3152e7b66c6b84d6'
            'b6c132360be5508bb01ae76193bea1b23ea80a6bbc73e4666687a6457188b597'
            '6f0c7f83b4728058d7d8adfb0baae3c529c99f259c2d9799bb31756ede472dcd')

build() {
  cd "${srcdir}/hollo-${pkgver}"
  NODE_ENV="production" pnpm install --frozen-lockfile
}

package() {
  # Add runtime-only dependencies
  depends+=(postgresql ffmpeg)

  install -dm 755 "${pkgdir}/usr/share/hollo"
  install -Dm 644 "${srcdir}/hollo.service" "${pkgdir}/usr/lib/systemd/system/hollo.service"
  install -Dm 644 "${srcdir}/hollo.sysusers" "${pkgdir}/usr/lib/sysusers.d/hollo.conf"
  install -Dm 644 "${srcdir}/hollo.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/hollo.conf"
  install -Dm 644 "${srcdir}/hollo.hook" "${pkgdir}/usr/share/libalpm/hooks/hollo.hook"

  local dirs=("bin" "drizzle" "scripts" "src" "node_modules")
  local files=("drizzle.config.ts" "package.json" "pnpm-lock.yaml" "tsconfig.json")
  for path in "${dirs[@]}"; do
    cp -dpr --no-preserve=ownership "${srcdir}/hollo-${pkgver}/${path}/" "${pkgdir}/usr/share/hollo"
  done
  for path in "${files[@]}"; do
    cp -dp --no-preserve=ownership "${srcdir}/hollo-${pkgver}/${path}" "${pkgdir}/usr/share/hollo"
  done

  find "${pkgdir}/usr/share/hollo" -type f -path '*/.bin/*' -print0 |
    xargs -0 sed -i "/export NODE_PATH/{s|${srcdir}/hollo-${pkgver}|/usr/share/hollo|g}"
}
