# Maintainer: haxibami <contact at haxibami dot net>

pkgname=ttf-san-francisco-jp
pkgver=1.0.0
pkgrel=2
pkgdesc="San Francisco font for Japanese, extracted from apple.com"
arch=('any')
url="https://www.apple.com/jp/"
license=('custom')
depends=('fontconfig' 'xorg-font-utils')
source=(
  'https://www.apple.com/wss/fonts/SF-Pro-JP/v1/SFProJP_thin.ttf'
  'https://www.apple.com/wss/fonts/SF-Pro-JP/v1/SFProJP_ultralight.ttf'
  'https://www.apple.com/wss/fonts/SF-Pro-JP/v1/SFProJP_light.ttf'
  'https://www.apple.com/wss/fonts/SF-Pro-JP/v1/SFProJP_regular.ttf'
  'https://www.apple.com/wss/fonts/SF-Pro-JP/v1/SFProJP_medium.ttf'
  'https://www.apple.com/wss/fonts/SF-Pro-JP/v1/SFProJP_semibold.ttf'
  'https://www.apple.com/wss/fonts/SF-Pro-JP/v1/SFProJP_bold.ttf'
  'https://www.apple.com/wss/fonts/SF-Pro-JP/v1/SFProJP_heavy.ttf'
  )

sha256sums=('a279d4921cd48207ebbc4af8488045180b6e4983402687d3069990ee5cfe1f0e'
            '68c1d0970d239d10b12704a8cf2996af1281245a75a64088937c05dca2e6bd52'
            '57576dbc595bbb0d020feca34459747eaecd773ec4bf87bdc04187f40ae5bed2'
            '30819482a5525a839dc1ac7c6d95837031619776b008976628fbcb4321c6068f'
            'bd201e8a59b8d8d895d74f12281f8c9bb42480e1fdace4b4e4ee3ce90d85f0c8'
            'c0592b66f2d6b3c16c5a84a973b6b0da00d5897dd2bb8214a825ce7223baa9a4'
            'efacae0d807e62185e2ddd94f202e83f331f800ac0f5c0c7df849afb854f857e'
            'd9d7488b342e2999414d9eed4f13e57b5be71340030e0f73c79b528953f344db')

DLAGENTS=(
  "http::/usr/bin/curl -A Mozilla -H Referer:https://www.apple.com/wss -fLC - --retry 3 --retry-delay 3 -o %o %u"
  "https::/usr/bin/curl -A Mozilla -H Referer:https://www.apple.com/wss -fLC - --retry 3 --retry-delay 3 -o %o %u"
  "${DLAGENTS[@]}"
)

package() {
  cd "${srcdir}"
  install -d "${pkgdir}/usr/share/fonts/TTF"
  install -m644 SFProJP_*.ttf "${pkgdir}/usr/share/fonts/TTF"
}
